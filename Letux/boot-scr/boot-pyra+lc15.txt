echo boot.scr for Pyra+LC15
setenv fdtfile omap5-letux-cortex15+pyra.dtb
setenv mmcdev 0		# mmc number as seen by u-boot where it looks for kernel
setenv kernelmmc 0	# mmc number as seen by kernel
setenv defaultdisplay   lcd
setenv PARTITION 1	# partition where kernel is found (simulates the partition selection mechanism of the GTA04)
setenv PARTITIONTYPE FAT

# set default
setenv mmcargs 'setenv bootargs elevator=noop console=ttyO2,115200n8 root=/dev/mmcblk${kernelmmc}p2 rw rootwait earlyprintk fixrtc'

# load device tree and kernel binary
echo ${fdtfile} to 0x825f0000
fatload mmc ${mmcdev}:${PARTITION} 0x825f0000 ${fdtfile}
echo uImage to 0x80300000
fatload mmc ${mmcdev}:${PARTITION} 0x80300000 uImage

run mmcargs

# allow to overwrite boot args (incl. kernelmmc)
echo bootargs.scr to 0x81300000
if fatload mmc ${mmcdev}:${PARTITION} 0x81300000 bootargs.scr
then
	source 0x81300000
fi
fatload mmc ${mmcdev}:${PARTITION} 0x80300000 uImage

setenv fdt_high 0x84000000
bootm 0x80300000 - 0x825f0000
